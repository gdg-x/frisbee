apply plugin: 'android'
apply plugin: 'versions'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

// Manifest version information!
def versionMajor = 1
def versionMinor = 2
def versionPatch = 2
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            runProguard true
            zipAlign true
            debuggable false
        }
        debug {
            debuggable true
            //packageNameSuffix '.dev'
            //versionNameSuffix '-dev'
        }
    }


    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.+'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'joda-time:joda-time:2.3'
    compile 'ch.acra:acra:4.5.0'
    compile 'com.jakewharton.timber:timber:2.3.+'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.9'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:extra-abc:0.9.9'
    compile ('de.keyboardsurfer.android.widget:crouton:1.8.4') {
        exclude module: 'support-v4'
    }
    compile ('com.github.chrisbanes.bitmapcache:library:2.3'){
        exclude module: 'support-v4'
    }
    compile 'com.google:volley:4.3'
    compile 'com.google.android.analytics:analytics:3'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.google.android:youtube-android-player-api:1.0.0'
    compile 'com.google.android.gms:play-services:4.4.+'
    compile 'com.google.http-client:google-http-client-gson:1.18.0-rc'
    compile 'com.google.apis:google-api-services-plus:v1-rev126-1.18.0-rc'
    compile 'com.google.apis:google-api-services-youtube:v3-rev106-1.18.0-rc'
    compile 'com.google.api-client:google-api-client-android:1.18.0-rc'
    compile 'com.android.support:appcompat-v7:19.1.+'
    compile 'com.android.support:mediarouter-v7:19.1.+'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.okhttp:okhttp:1.6.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:1.6.+'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.squareup.picasso:picasso:2.3.0'
    compile 'com.jakewharton:butterknife:5.1.0'
    compile 'com.google.zxing:android-integration:3.1.+'
    compile 'com.google.zxing:core:3.1.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
